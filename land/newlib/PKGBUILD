pkgname=newlib
pkgver=3.2.0
pkgrel=9
pkgdesc="Newlib is a C library intended for use on embedded systems."
arch=('i786')
url="https://sourceware.org/newlib/"
license=('GPL')
source=(
	"http://sourceware.org/pub/$pkgname/$pkgname-$pkgver.tar.gz"
	"$pkgname-$pkgver.patch"
)
sha256sums=(
	'f7b2322964a1e3b37bec0768da20f50dfb62247d729110974dd95756a53bb6d4'
	'SKIP'
)

prepare() {
	patch -p0 < $pkgname-$pkgver.patch

	cd $pkgname-$pkgver
	cp -r ../../xelix newlib/libc/sys

	cd newlib/libc/sys/xelix/
	aclocal-1.11 -I ../../../ -I ../../../../
	autoconf-2.69
	automake-1.11 --cygnus Makefile

	cd ..
	aclocal-1.11 -I ../.. -I ../../..
	autoconf-2.69
	automake-1.11 --cygnus Makefile
}

build() {
	cd $pkgname-$pkgver

	# Shared/position-independent build
	CFLAGS_FOR_TARGET="-fPIC" CFLAGS="" CPPFLAGS="" LDFLAGS="" ./configure \
		--target=i786-pc-xelix \
		--prefix=/usr \
		--exec-prefix=/usr \
		--sysconfdir=/etc \
		--enable-newlib-mb \
		--enable-newlib-iconv \
		--enable-newlib-io-c99-formats \
		--enable-newlib-io-long-long \
		--enable-newlib-io-long-double
	make clean

	make -j$(nproc) all-target-newlib
	i786-pc-xelix-gcc -shared -Wl,--whole-archive -Wl,--allow-multiple-definition -nodefaultlibs -ffreestanding -o libc.so i786-pc-xelix/newlib/libc.a -lgcc_s

	# Static build
	make distclean
	CFLAGS_FOR_TARGET="" CFLAGS="" CPPFLAGS="" LDFLAGS="" ./configure \
		--target=i786-pc-xelix \
		--prefix=/usr \
		--exec-prefix=/usr \
		--sysconfdir=/etc \
		--enable-newlib-mb \
		--enable-newlib-iconv \
		--enable-newlib-io-c99-formats \
		--enable-newlib-io-long-long \
		--enable-newlib-io-long-double

	make -j$(nproc) all-target-newlib
}

package() {
	cd $pkgname-$pkgver
	make DESTDIR=$pkgdir install-target-newlib
	mv $pkgdir/usr/i786-pc-xelix/* $pkgdir/usr/
	rmdir $pkgdir/usr/i786-pc-xelix

	cp libc.so $pkgdir/usr/lib/libc.so.1
	ln -s libc.so.1 $pkgdir/usr/lib/libc.so

	# Some GNU packages expect xlocale.h to exist
	ln -s locale.h $pkgdir/usr/include/xlocale.h

	# openlibm is used instead
	rm $pkgdir/usr/lib/libm.a
	
	# Conflict with ncurses
	rm $pkgdir/usr/include/unctrl.h
}
